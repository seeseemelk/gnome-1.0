pkgname=imlib
pkgver=1.9.4
pkgrel=3
pkgdesc="An old version of imlib"
arch=(x86_64)
url=""
depends=(glib-old libx11 libxext libpng)
license=('GPL')
source=(
	"https://download.gnome.org/desktop/1.0/1.0/sources/imlib-1.9.4.tar.gz"
	"configure.patch"
	"fix-build-errors.patch"
	"fix-open-missing-mode.patch"
	"fix-ltconfig.patch"
)
md5sums=('8d0e50e28a5b203ff76f114bb38627c7'
         '6478976838c0419de12b0a8b7fffafd4'
         'c6ac6f9647743b0d1373437b6100cf37'
         'f811e6c4e7370a4f4f8a1d0d1a583d5e'
         'ec70a78265e8c81c9c8713b7c1ad1dd4')

prepare() {
	cd "imlib-$pkgver"
	patch -p1 -i "$srcdir/configure.patch"
	patch -p1 -i "$srcdir/fix-build-errors.patch"
	patch -p1 -i "$srcdir/fix-open-missing-mode.patch"
	patch -p1 -i "$srcdir/fix-ltconfig.patch"
}

build() {
	cd "imlib-$pkgver"
	./configure \
		--prefix=/usr \
		--disable-static
	make
}

check() {
	cd "imlib-$pkgver"
	make -k check
}

package() {
	cd "imlib-$pkgver"
	make DESTDIR="$pkgdir/" install
}
