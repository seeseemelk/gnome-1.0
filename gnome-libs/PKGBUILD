pkgname=gnome-libs
pkgver=1.0.16
pkgrel=1
pkgdesc="An old version of Gnome Libs"
arch=(x86_64)
url=""
license=('GPL')
depends=('db' 'imlib' 'audiofile-old')
source=(
	"https://download.gnome.org/desktop/1.0/1.0/sources/gnome-libs-1.0.16.tar.gz"
	"configure.patch"
	"configure-db185.patch"
	"fix-libpng.patch"
	"fix-colors.patch"
	"fix-loadpng.patch"
)
md5sums=('7bbaf7b947326ca8d42beca74ae35919'
         'cada285d91bb2443b93e882d8e06641d'
         '91ba5ecb15e0c94a48ac4c17475117ca'
         '368832242792ed3078832f03726b7a24'
         'e229c67cc3bcf49f89a0ab4f1723c115'
         'b151bc97b81e8455de68dbc9fda74416')

prepare() {
	cd "gnome-libs-$pkgver"
	patch -p1 -i "$srcdir/configure.patch"
	patch -p1 -i "$srcdir/configure-db185.patch"
	patch -p1 -i "$srcdir/fix-libpng.patch"
	patch -p1 -i "$srcdir/fix-colors.patch"
	patch -p1 -i "$srcdir/fix-loadpng.patch"
}

build() {
	cd "gnome-libs-$pkgver"
	CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2}
	./configure \
		--prefix=/usr
	make
}

check() {
	cd "gnome-libs-$pkgver"
	make -k check
}

package() {
	cd "gnome-libs-$pkgver"
	make DESTDIR="$pkgdir/" install
}
