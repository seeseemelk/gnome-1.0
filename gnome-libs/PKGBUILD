pkgname=gnome-libs
pkgver=1.0.16
pkgrel=3
pkgdesc="An old version of Gnome Libs"
arch=(x86_64)
url=""
license=('GPL')
depends=(db imlib-gdk audiofile-old gtk-old orbit libxpm)
options=(!buildflags)
source=(
	"https://download.gnome.org/desktop/1.0/1.0/sources/gnome-libs-1.0.16.tar.gz"
	"configure.patch"
	"configure-db185.patch"
	"fix-ltconfig.patch"
	"fix-libpng.patch"
	"fix-colors.patch"
	"fix-loadpng.patch"
	"fix-icon-entry.patch"
)
md5sums=('7bbaf7b947326ca8d42beca74ae35919'
         'cada285d91bb2443b93e882d8e06641d'
         '91ba5ecb15e0c94a48ac4c17475117ca'
         'ec70a78265e8c81c9c8713b7c1ad1dd4'
         'dcf7122d84408fc3ed6d6c4036a60ea9'
         'e229c67cc3bcf49f89a0ab4f1723c115'
         'b151bc97b81e8455de68dbc9fda74416'
         '0f09a3d8fa132b4dfdd4d3cd79981cf0')

prepare() {
	cd "gnome-libs-$pkgver"
	patch -p1 -i "$srcdir/configure.patch"
	patch -p1 -i "$srcdir/configure-db185.patch"
	patch -p1 -i "$srcdir/fix-ltconfig.patch"
	patch -p1 -i "$srcdir/fix-libpng.patch"
	patch -p1 -i "$srcdir/fix-colors.patch"
	patch -p1 -i "$srcdir/fix-loadpng.patch"
	patch -p1 -i "$srcdir/fix-icon-entry.patch"
}

build() {
	cd "gnome-libs-$pkgver"
	CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
	./configure \
		--prefix=/usr \
		--disable-static
	make
}

check() {
	cd "gnome-libs-$pkgver"
	make -k check
}

package() {
	cd "gnome-libs-$pkgver"
	make DESTDIR="$pkgdir/" install
	rm -rf "$pkgdir/home"
	mv "$pkgdir/usr/sbin/gnome-pty-helper" "$pkgdir/usr/bin"
	rm -rf "$pkgdir/usr/sbin"
}
