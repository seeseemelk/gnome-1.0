pkgname=gnome-games
pkgver=1.0.2
pkgrel=1
epoch=
pkgdesc="An old version of gedit"
arch=(x86_64)
url=""
license=('GPL')
depends=(gnome-libs)
source=(
	"https://download.gnome.org/desktop/1.0/1.0/sources/gnome-games-1.0.2.tar.gz"
	"configure.patch"
	"fix-build-errors.patch"
	"fix-same-gnome-install.patch"
	"fix-gnobots-install.patch"
)
md5sums=('1fbe5076d9234522f6f6401e672a02fe'
         'cada285d91bb2443b93e882d8e06641d'
         '191f71f74a1fbdc3a76068995a351df6'
         '1c315de0e3b6d6ee801a871f53f2ecee'
         'c9d2698aa1566ebf876df51c74109daa')

prepare() {
	cd "gnome-games-$pkgver"
	patch -p1 -i "$srcdir/configure.patch"
	patch -p1 -i "$srcdir/fix-build-errors.patch"
	patch -p1 -i "$srcdir/fix-same-gnome-install.patch"
	patch -p1 -i "$srcdir/fix-gnobots-install.patch"
}

build() {
	cd "gnome-games-$pkgver"
	CPPFLAGS=${CPPFLAGS/-D_FORTIFY_SOURCE=2/}
	./configure \
		--prefix=/usr \
		--disable-static
	make
}

check() {
	cd "gnome-games-$pkgver"
	make -k check
}

package() {
	cd "gnome-games-$pkgver"

	cd gnomine
	install -Dm755 gnomine "$pkgdir/usr/bin/gnomine"
	cd ..

	cd gnobots
	install -Dm755 gnobots "$pkgdir/usr/bin/gnobots"
	install -dm755 "$pkgdir/usr/share/pixmaps/gnobots"
	install -m644 -t "$pkgdir/usr/share/pixmaps/gnobots" \
		aieee.png cows.png eggs.png gnomes.png mice.png \
		robots.png windows.png yahoo.png
	install -m644 -t "$pkgdir/usr/share/pixmaps" gnome-gnobots.png
	cd ..

	cd gnobots2
	# Executable
	install -Dm755 gnobots2 "$pkgdir/usr/bin/gnobots2"
	# Pixmaps
	install -dm755 "$pkgdir/usr/share/pixmaps/gnobots2"
	install -m644 -t "$pkgdir/usr/share/pixmaps/gnobots2" \
		gnomes.png cows.png eggs.png mice.png robots.png \
		yahoo.png aieee.png splat.png
	# App icon
	install -m644 -t "$pkgdir/usr/share/pixmaps" gnome-gnobots2.png
	# Configs
	install -dm755 "$pkgdir/usr/share/gnobots2"
	install -m644 -t "$pkgdir/usr/share/gnobots2" \
		robots2.cfg robots2_easy.cfg classic_robots.cfg \
		robots_with_safe_teleport.cfg nightmare.cfg
	# Sound lists
	install -Dm644 gnobots2.soundlist "$pkgdir/usr/etc/sound/events/gnobots2.soundlist"
	# Sounds
	install -dm755 "$pkgdir/usr/share/sounds/gnobots2"
	install -m644 -t "$pkgdir/usr/share/sounds/gnobots2" \
		bad.wav die.wav splat.wav teleport.wav victory.wav yahoo.wav
	cd ..
}
